class axi4l_monitor extends uvm_monitor;
  `uvm_component_utils(axi4l_monitor)

  virtual axi4l_if.MON vif;
  uvm_analysis_port #(axi4l_seq_item) ap;

  function new(string name="axi4l_monitor", uvm_component parent=null);
    super.new(name, parent);
    ap = new("ap", this);
  endfunction

  virtual function void build_phase(uvm_phase phase);
    super.build_phase(phase);
    if (!uvm_config_db #(virtual axi4l_if.MON)::get(this, "", "vif", vif))
      `uvm_fatal(get_type_name(), "Virtual interface get failed")
  endfunction

  virtual task run_phase(uvm_phase phase);
    wait (vif.ARESETn === 1'b1);
    fork 
      collect_writes(); 
      collect_reads();
      reset_watcher(); 
    join
  endtask

  virtual task collect_writes();
    forever begin
      axi4l_seq_item txn = axi4l_seq_item::type_id::create("txn");
      txn.txn_sel = (1 << `TXN_BIT_WRITE); 
      fork
        begin
          while (vif.mon_cb.AWVALID !== 1'b1 || vif.mon_cb.AWREADY !== 1'b1) @(vif.mon_cb);
          txn.AWADDR = vif.mon_cb.AWADDR; 
          txn.AWPROT = vif.mon_cb.AWPROT;
          `uvm_info("MON_TRACE", "ADDRESS HANDSHAKE DONE", UVM_FULL)
        end
        begin
          while (vif.mon_cb.WVALID !== 1'b1 || vif.mon_cb.WREADY !== 1'b1) @(vif.mon_cb);
          txn.DATA  = vif.mon_cb.WDATA; 
          txn.WSTRB = vif.mon_cb.WSTRB;
          `uvm_info("MON_TRACE", "DATA HANDSHAKE DONE", UVM_FULL)
        end
      join
      while (vif.mon_cb.BVALID !== 1'b1 || vif.mon_cb.BREADY !== 1'b1) @(vif.mon_cb);
      txn.RESP = vif.mon_cb.BRESP;
         `uvm_info("MON_TRACE", "Transaction captured", UVM_FULL)
      ap.write(txn); 
    end
  endtask

  virtual task collect_reads();
    forever begin
      axi4l_seq_item txn = axi4l_seq_item::type_id::create("txn");
      txn.txn_sel = (1 << `TXN_BIT_READ); 
      while (vif.mon_cb.ARVALID !== 1'b1 || vif.mon_cb.ARREADY !== 1'b1) @(vif.mon_cb);
      txn.ARADDR = vif.mon_cb.ARADDR; 
      txn.ARPROT = vif.mon_cb.ARPROT;
      `uvm_info("MON_TRACE", "ADDRESS captured", UVM_FULL)
      while (vif.mon_cb.RVALID !== 1'b1 || vif.mon_cb.RREADY !== 1'b1) @(vif.mon_cb);
      txn.RDATA = vif.mon_cb.RDATA; 
      txn.RESP  = vif.mon_cb.RRESP;
         `uvm_info("MON_TRACE", "Read captured", UVM_FULL)
      ap.write(txn);
    end
  endtask
  
  virtual task reset_watcher();
	  forever begin
	  @(negedge vif.ARESETn);
	  `uvm_info("MON", "Mid sim reset detected killing all the threads", UVM_FULL)
	  disable collect_reads;
	  disable collect_writes;
	  @(posedge vif.ARESETn);
		`uvm_info("MON", "Mid sim reset deasserted starting all the threads", UVM_FULL)
	  fork
		 collect_reads();
		 collect_writes(); 
	  join_none
  	end
  endtask
  
endclass
